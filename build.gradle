plugins {
    id("java")
    id("com.github.johnrengelman.shadow") version '7.1.2'
}

group = "fr.hokib"
version = "1.2.4"

repositories {
    mavenCentral()

    //PaperMc
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    //BentoBox
    maven {
        url = "https://repo.codemc.org/repository/maven-snapshots/"
    }

    //SuperiorSkyblock
    maven {
        url = "https://repo.bg-software.com/repository/api/"
    }

    maven {
        url = "https://ci.ender.zone/plugin/repository/everything/"
    }

    maven { url 'https://jitpack.io' }
}

dependencies {

    implementation 'com.saicone.ezlib:ezlib:1.1'

    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly "world.bentobox:bentobox:1.21.0-SNAPSHOT"
    compileOnly "com.bgsoftware:SuperiorSkyblockAPI:2023.3"
    compileOnly "com.massivecraft:Factions:1.6.9.5-U0.6.21"

    compileOnly("org.mongodb:mongodb-driver-sync:4.9.1")
    compileOnly("com.zaxxer:HikariCP:5.0.1")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
    options.encoding = "UTF-8"

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    destinationDirectory.set(file("out"))
    relocate 'com.saicone.ezlib', project.group + '.hdrawer.lib.ezlib'
    relocate 'com.mongodb', project.group + '.hdrawer.lib.mongodb'
    relocate 'com.zaxxer.hikari', project.group + '.hdrawer.lib.hikari'
    archiveName = "${project.name}-${project.version}.jar"
    minimize()
}